plugins {
    id 'base'
}

repositories {
    ['cloudark', 'localhost'].each { host ->
        maven {
            credentials {
                username findProperty('cloudarkUser')
                password findProperty('cloudarkApiKey')
            }
            url "http://$host:8081/artifactory/local/"
        }
    }
}

configurations {
    data
}

dependencies {
    data group: 'de.dfki.mary', name: 'blizzard2017-data-lab', version: '0.4.0', ext: 'zip'
    data group: 'de.dfki.mary.blizzard', name: 'blizzard2017-data-f0', version: '0.1-SNAPSHOT', ext: 'zip'
    data group: 'de.dfki.mary.blizzard', name: 'blizzard2017-f0-dnn-model', version: '0.1-SNAPSHOT', ext: 'zip'
}

task unpackData(type: Copy) {
    from configurations.data
    into "$buildDir/input"
    filesMatching '*.zip', { zipFileDetails ->
        copy {
            from zipTree(zipFileDetails.file)
            into destinationDir
            eachFile {
                it.path = it.name
            }
            includeEmptyDirs = false
        }
        zipFileDetails.exclude()
    }
}

task runSynthesis(type: Exec) {
    dependsOn unpackData
    def pyFile = "08_inference.py"
    inputs.files pyFile
    def destDir = file("$buildDir/output")
    outputs.dir destDir
    commandLine 'python3', pyFile, unpackData.destinationDir, destDir
    doFirst {
        destDir.mkdirs()
    }
}
